buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.github.nosepass.todonotifier"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        // This enables the new runner instead of the old ActivityInstrumentationTestCase2 style one
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:support-annotations:22.1.1'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'nl.qbusict:cupboard:2.1.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    //androidTestCompile 'com.android.support.test:runner:0.2' // this gets included by rules:0.2
    androidTestCompile('com.android.support.test:rules:0.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        // 22.0 instead of 22.1 is imported here
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
        // 22.0 instead of 22.1 is imported here
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
        // 22.0 instead of 22.1 is imported here
    }
    testCompile 'junit:junit:4.12'
    debugCompile('org.mockito:mockito-core:1.9.5') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
        // gradle is complaining about this conflicting with 1.3
    }
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
}
